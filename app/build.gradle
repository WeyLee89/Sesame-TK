import java.security.MessageDigest
import java.text.SimpleDateFormat


plugins {
    id "com.android.application"
}

android {
    buildFeatures {
        buildConfig = true
    }

    def major = 0          // 主版本号
    def minor = 2          // 次版本号
    def patch = 5          // 补丁版本号
    def buildTag = "beta.fix.1" // 构建标签 (alpha, beta, release, 等)
    def buildDate
    def buildDate2
    def buildTime
    def buildTime2
    def buildTargetCode
    def _versionCode
    def _versionName
    try {
        def dateFormat = new SimpleDateFormat("yy-MM-dd")
        buildDate = dateFormat.format(new Date())
        buildDate2 = dateFormat.format(new Date()).replace("-", ".")
        def timeFormat = new SimpleDateFormat("HH:mm:ss")
        buildTime = timeFormat.format(new Date())
        buildTime2 = timeFormat.format(new Date()).replace(":", ".")
        MessageDigest md = MessageDigest.getInstance("MD5")
        md.update(buildTime.bytes)
        buildTargetCode = md.digest().encodeHex().toString().substring(0, 4)
    }
    catch (Exception e) {
        println("时间或日期格式化错误: ${e.message}")
        buildDate = "unknown"
        buildTime = "unknown"
        buildTime2 = "unknown"
        buildDate2 = "unknown"
        buildTargetCode = "0000"
    }
    _versionCode = 'git rev-list --count HEAD'.execute().text.trim().toInteger()
    if (buildTag.contains("alpha") || buildTag.contains("beta")) {
        _versionName = "${major}.${minor}.${patch}-${buildTag}"
    } else if (buildTag == "debug") {
        _versionName = "${buildTargetCode}-${buildTag}"
    } else {
        _versionName = "${major}.${minor}.${patch}-${buildTag}"
        buildTag = ""
    }

    namespace "fansirsqi.xposed.sesame"
    compileSdk 34
    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode _versionCode
        versionName _versionName
        buildConfigField "String", "BUILD_DATE", "\"${buildDate}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "BUILD_NUMBER", "\"${buildTargetCode}\""
        buildConfigField "String", "BUILD_TAG", "\"${buildTag}\""
    }

    ext {
        applicationName = "Sesame"
        applicationType = "Normal"
    }
    flavorDimensions.add("default")
    productFlavors {
        normal {
            dimension "default"
            ext.applicationType = "Normal"
        }
        compatible {
            dimension "default"
            ext.applicationType = "Compatible"
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig null  // 禁用签名
        }
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig null  // 禁用签名
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                if (variant.buildType.name == 'release') {
                    output.outputFileName = "${applicationName}-${variant.productFlavors[0].ext.applicationType}-${defaultConfig.versionName}.apk"
                }
                if (variant.buildType.name == 'debug') {
                    output.outputFileName = "${applicationName}-${variant.productFlavors[0].ext.applicationType}-${defaultConfig.versionName}-debug.${buildTargetCode}.apk"
                }
            }
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.10'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'com.google.android.material:material:1.12.0'
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.18.1'
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.1'
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.18.1'
    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.5'
    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.5'
    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.5'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.18.1'
}
